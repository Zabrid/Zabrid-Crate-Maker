# ###################################################################

# Zadzrfbrid's Crate Maker
# Source Code

# Feel free to change anything below this point.
# Although, beware you might break something!

# If you make a change and think that it should be pushed to the official script
# make a pull request on the GitHub https://github.com/Zabrid/Zabrid-Crate-Maker

# ###################################################################

# Main /crates GUI
function zcratemaker_gui_main(p: player):
    set {_uuid} to uuid of {_p}

    set {_gui-name::*} to zcratemaker_getconfig("gui [name]")
    set {_gui-size::*} to zcratemaker_getconfig("gui [size]")
    set {_gui-fill::*} to zcratemaker_getconfig("gui [fill-item]")
    set {_gui} to chest inventory named {_gui-name::1} with {_gui-size::1} rows
    set slot (all integers between 0 and 54) of {_gui} to {_gui-fill::1}

    loop {zcratemaker::crates::*}:
        set {_crate} to loop-value
        {zcratemaker::crates::%{_crate}%::guicrateshow} is true
        set {_item} to {zcratemaker::crates::%{_crate}%::guicrateitem} with all flags hidden
        set {_slot} to {zcratemaker::crates::%{_crate}%::guicrateslot}
        set {_name} to {zcratemaker::crates::%{_crate}%::guicratename}
        set {_lore::*} to {zcratemaker::crates::%{_crate}%::guicratelore} split by "||"
        replace all "{keys}" in {_lore::*} with "%{zcratemaker::crates::keys::%{_crate}%::%{_uuid}%} ? 0%"
        replace all "{keys}" in {_name} with "%{zcratemaker::crates::keys::%{_crate}%::%{_uuid}%} ? 0%"
        set slot {_slot} of {_gui} to 1 of {_item} named {_name} with lore {_lore::*}
    
    open {_gui} to {_p}

# Preview Crate GUI
function zcratemaker_gui_preview(p: player, crate: text, type: text):
    {zcratemaker::crates::%{_crate}%} is set

    set {_gui-name::*} to zcratemaker_getconfig("preview [name]")
    set {_gui-size::*} to zcratemaker_getconfig("preview [size]")
    set {_gui-fill::*} to zcratemaker_getconfig("preview [fill-item]")
    set {_gui} to chest inventory named {_gui-name::1} with {_gui-size::1} rows
    set slot (all integers between 0 and 54) of {_gui} to {_gui-fill::1}

    if {_type} is "main":
        set {_back-slot::*} to zcratemaker_getconfig("preview [back-slot]")
        set {_back-item::*} to zcratemaker_getconfig("preview [back-item]")
        set slot {_back-slot::1} of {_gui} to {_back-item::1} with nbt "{backbutton:1}"

    set {_slot} to -1
    loop {zcratemaker::crates::%{_crate}%::items::*}:
        add 1 to {_slot}

        set {_chance} to tag "zcratemaker-chance" of loop-value's nbt
        set {_lore::*} to zcratemaker_getconfig("preview [reward-item-lore]")
        replace all "{chance}" in {_lore::*} with "%{_chance}%"

        set slot {_slot} of {_gui} to loop-value with lore {_lore::*}

    open {_gui} to {_p}

# Edit GUI
function zcratemaker_gui_edit(p: player, crate: text):
    {zcratemaker::crates::%{_crate}%} is set
    set {_crate} to {zcratemaker::crates::%{_crate}%}
    set {_gui} to chest inventory named "&8Zabrid's Crate Maker | &6&l%{_crate}%" with 3 rows
    set {_gui} to zcratemaker_bordergui({_gui}, 3, 1 of grey stained glass pane named "&8Zabrid's Crate Maker")

    # Crate Key Customization Display
    set {_key-item} to {zcratemaker::crates::%{_crate}%::cratekeyitem}
    set {_key-name} to {zcratemaker::crates::%{_crate}%::cratekeyname}
    set {_key-lore::*} to {zcratemaker::crates::%{_crate}%::cratekeylore} split by "||"
    loop {_key-lore::*}:
        set {_key-lore::%loop-index%} to "&6 - &f%loop-value%"
    set slot 12 of {_gui} to 1 of tripwire hook named "&eCrate Key Customization" with lore "&8Zabrid's Crate Maker" and "" and "&eItem: &f%{_key-item}%" and "" and "&eName: &f%{_key-name}%" and "" and "&eLore:" and {_key-lore::*} and "" and "&bLeft Click to edit the crate key item."

    # GUI Customization
    set {_slot} to {zcratemaker::crates::%{_crate}%::guicrateslot}
    set {_item} to {zcratemaker::crates::%{_crate}%::guicrateitem}
    set {_name} to {zcratemaker::crates::%{_crate}%::guicratename}
    set {_lore::*} to {zcratemaker::crates::%{_crate}%::guicratelore} split by "||"
    loop {_lore::*}:
        set {_lore::%loop-index%} to "&6 - &f%loop-value%"
    set slot 13 of {_gui} to 1 of ender chest named "&eCrate GUI Customization" with lore "&8Zabrid's Crate Maker" and "" and "&7Customize the item representing" and "&7this crate in the &n/crates&7 gui." and "" and "&eSlot: &f%{_slot}%" and "" and "&eItem: &f%{_item}%" and "" and "&eName: &f%{_name}%" and "" and "&eLore:" and {_lore::*} and "" and "&bLeft Click to edit the crate gui item."

    # Reward Customization
    set {_items} to size of {zcratemaker::crates::%{_crate}%::items::*}
    set slot 14 of {_gui} to 1 of bone meal named "&eCrate Reward Customization" with lore "&8Zabrid's Crate Maker" and "" and "&7Edit the items this crate" and "&7rewards for opening it." and "" and "&eItem Amount: &f%{_items}%" and "" and "&bLeft click to modify crate rewards."

    open {_gui} to {_p}

# Crate Key Edit GUI
function zcratemaker_gui_editcratekey(p: player, crate: text):
    {zcratemaker::crates::%{_crate}%} is set
    set {_crate} to {zcratemaker::crates::%{_crate}%}
    set {_gui} to chest inventory named "&8Crate Key Edit | &6&l%{_crate}%" with 3 rows
    set {_gui} to zcratemaker_bordergui({_gui}, 3, 1 of grey stained glass pane named "&8Zabrid's Crate Maker")

    set slot 26 of {_gui} to 1 of birch door named "&cReturn to previous menu."

    set {_item} to {zcratemaker::crates::%{_crate}%::cratekeyitem}
    set slot 12 of {_gui} to 1 of {_item} with all flags hidden named "&6Edit Crate Key Item" with lore "&8Zabrid's Crate Maker" and "" and "&7Edit the item of this crate key." and "" and "&eItem: &f%{_item}%" and "" and "&bLeft click to set the item to your held item."

    set {_name} to {zcratemaker::crates::%{_crate}%::cratekeyname}
    set slot 13 of {_gui} to 1 of empty map with all flags hidden named "&6Edit Crate Key Name" with lore "&8Zabrid's Crate Maker" and "" and "&7Edit the name of this crate key." and "" and "&eName: &f%{_name}%" and "" and "&bLeft click to set the name of the crate key."

    set {_2lore::*} to {zcratemaker::crates::%{_crate}%::cratekeylore} split by "||"
    loop {_2lore::*}:
        set {_lore::%loop-index%} to "&6 - &f%loop-value%"
    set slot 14 of {_gui} to 1 of wither rose with all flags hidden named "&6Edit Crate Key Lore" with lore "&8Zabrid's Crate Maker" and "" and "&7Edit the lore of this crate key." and "" and "&eLore:" and {_lore::*} and "" and "&bLeft click to set the lore of the crate key."

    set slot 4 of {_gui} to 1 of {_item} named {_name} with lore {_2lore::*}

    open {_gui} to {_p}

function zcratemaker_gui_itemeditor(p: player, crate: text):
    {zcratemaker::crates::%{_crate}%} is set
    set {_gui} to chest inventory named "&8Crate Item Editor | &6&l%{_crate}%" with 6 rows

    set slot 45 of {_gui} to 1 of birch door named "&cReturn to previous menu."
    
    set {_slot} to -1
    loop {zcratemaker::crates::%{_crate}%::items::*}:
        add 1 to {_slot}

        set {_chance} to tag "zcratemaker-chance" of loop-value-1's nbt
        set {_commands} to tag "zcratemaker-command" of loop-value-1's nbt
        set {_commands::*} to {_commands} split by "||"
        set {_lore::*} to zcratemaker_getconfig("preview [reward-item-lore]")
        replace all "{chance}" in {_lore::*} with "%{_chance}%"
        if {_commands::*} is set:
            loop {_commands::*}:
                set {_commands::%loop-index-2%} to "&6 - &f/%loop-value-2%"
            set slot {_slot} of {_gui} to loop-value with lore {_lore::*} and "&8-------------------------------------" and "" and "&6Commands:" and {_commands::*} and "" and "&8-------------------------------------" and "&bClick to remove this item from the crate."

        else:
            set slot {_slot} of {_gui} to loop-value with lore {_lore::*} and "&8-------------------------------------" and "&bClick to remove this item from the crate."

    open {_gui} to {_p}

function zcratemaker_gui_editcrategui(p: player, crate: text):
    {zcratemaker::crates::%{_crate}%} is set
    set {_gui} to chest inventory named "&8Crate Gui Edit | &6&l%{_crate}%" with 3 rows
    set {_gui} to zcratemaker_bordergui({_gui}, 3, 1 of grey stained glass pane named "&8Zabrid's Crate Maker")

    set slot 22 of {_gui} to 1 of birch door named "&cReturn to previous menu."

    set {_item} to {zcratemaker::crates::%{_crate}%::guicrateitem}
    set {_name} to {zcratemaker::crates::%{_crate}%::guicratename}
    set {_slot} to {zcratemaker::crates::%{_crate}%::guicrateslot}
    set {_lore::*} to {zcratemaker::crates::%{_crate}%::guicratelore} split by "||"
    replace all "{keys}" in {_lore::*} with "0"
    replace all "{keys}" in {_name} with "0"
    set slot 4 of {_gui} to 1 of {_item} named {_name} with lore {_lore::*}

    if {zcratemaker::crates::%{_crate}%::guicrateshow} is false:
        set {_showitem} to red dye
        set {_toggle} to "&cDisabled"
    else:
        set {_showitem} to lime dye
        set {_toggle} to "&aEnabled"
    set slot 11 of {_gui} to 1 of {_showitem} named "&6Toggle Crate Appearance" with lore "&8Zabrid's Crate Maker" and "" and "&7Toggle whether or not this crate" and "&7shows up in the &n/crates&7 menu." and "" and "&7Toggle: %{_toggle}%" and "" and "&bLeft click to toggle crate appearance."
    set slot 12 of {_gui} to 1 of {_item} named "&6Edit Crate Gui Item" with lore "&8Zabrid's Crate Maker" and "" and "&7Edit the item of the crate gui." and "" and "&eItem: &f%{_item}%" and "" and "&bLeft click to set the item to your held item."
    set slot 13 of {_gui} to 1 of empty map with all flags hidden named "&6Edit Crate Gui Name" with lore "&8Zabrid's Crate Maker" and "" and "&7Edit the name of the crate gui item." and "" and "&eName: &f%{_name}%" and "" and "&bLeft click to set the name of the crate gui item."
    set {_lore::*} to {zcratemaker::crates::%{_crate}%::guicratelore} split by "||"
    loop {_lore::*}:
        set {_lore::%loop-index%} to "&6 - &f%loop-value%"
    set slot 14 of {_gui} to 1 of wither rose with all flags hidden named "&6Edit Crate Gui Lore" with lore "&8Zabrid's Crate Maker" and "" and "&7Edit the lore of the crate gui item." and "" and "&eLore:" and {_lore::*} and "" and "&bLeft click to set the lore of the crate gui item."
    set slot 15 of {_gui} to 1 of spyglass named "&6Edit Crate Gui Slot" with lore "&8Zabrid's Crate Maker" and "" and "&7Edit the slot of the crate gui item." and "" and "&eSlot: &f%{_slot}%" and "" and "&bLeft click to set the lore of the crate gui slot."

    open {_gui} to {_p}

# Inventory Click
on inventory click:
    set {_gui-name::*} to zcratemaker_getconfig("gui [name]")
    set {_uuid} to player's uuid
    if name of player's current inventory is {_gui-name::1}:
        cancel event
        clicked inventory is player's current inventory
        loop {zcratemaker::crates::*}:
            set {_slot} to {zcratemaker::crates::%loop-value%::guicrateslot}
            set {_crate} to loop-value
            if index of clicked slot is {_slot}:
                if click action is left mouse button:
                    if {zcratemaker::crates::keys::%{_crate}%::%{_uuid}%} is greater than 0:
                        subtract 1 from {zcratemaker::crates::keys::%{_crate}%::%{_uuid}%}
                        if {zcratemaker::crates::keys::%{_crate}%::%{_uuid}%} is 0:
                            delete {zcratemaker::crates::keys::%{_crate}%::%{_uuid}%}
                        zcratemaker_opencrate(player, {_crate})
                    else:
                        set {_message::*} to zcratemaker_getlang("not-enough-keys")

                        set {_reward} to loop-value-1's name
                        set {_crate} to {zcratemaker::crates::%{_crate}%::guicratename}
                        set {_prefix} to "%zcratemaker_getconfig("prefix")%"

                        replace all "{prefix}" in {_message::*} with {_prefix}
                        replace all "{crate}" in {_message::*} with {_crate}
                        replace all "{player}" and "{p}" in {_message::*} with "%player%"
                        send {_message::*} to player
                else if click action is right mouse button:
                    zcratemaker_gui_preview(player, loop-value, "main")
                else if click action is middle mouse button:
                    if player does not have permission "zcratemaker.openall":
                        send zcratemaker_formatbasicmessage("%sender%", zcratemaker_getlang("no-perms")) 
                        stop      
                    if {zcratemaker::crates::keys::%{_crate}%::%{_uuid}%} is greater than 0:
                        loop {zcratemaker::crates::keys::%{_crate}%::%{_uuid}%} times:
                            subtract 1 from {zcratemaker::crates::keys::%{_crate}%::%{_uuid}%}
                            if {zcratemaker::crates::keys::%{_crate}%::%{_uuid}%} is 0:
                                delete {zcratemaker::crates::keys::%{_crate}%::%{_uuid}%}
                            zcratemaker_opencrate(player, {_crate})
                    else:
                        set {_message::*} to zcratemaker_getlang("not-enough-keys")

                        set {_reward} to loop-value-1's name
                        set {_crate} to {zcratemaker::crates::%{_crate}%::guicratename}
                        set {_prefix} to "%zcratemaker_getconfig("prefix")%"

                        replace all "{prefix}" in {_message::*} with {_prefix}
                        replace all "{crate}" in {_message::*} with {_crate}
                        replace all "{player}" and "{p}" in {_message::*} with "%player%"
                        send {_message::*} to player
    set {_gui-name::*} to zcratemaker_getconfig("preview [name]")
    if name of player's current inventory is {_gui-name::1}:
        cancel event
        clicked inventory is player's current inventory
        set {_back-slot::*} to zcratemaker_getconfig("preview [back-slot]")
        if index of clicked slot is {_back-slot::1}:
            set {_back} to tag "backbutton" of clicked slot's nbt
            {_back} is set
            zcratemaker_gui_main(player)


    player has permission "zcratemaker.edit"
    if name of player's current inventory contains "&8Zabrid's Crate Maker | &6&l":
        cancel event
        clicked inventory is player's current inventory
        set {_crate} to name of player's current inventory
        replace all "&8Zabrid's Crate Maker | &6&l" in {_crate} with ""
        if index of clicked slot is 12:
            zcratemaker_gui_editcratekey(player, {_crate})
        else if index of clicked slot is 13:
            zcratemaker_gui_editcrategui(player, {_crate})
        else if index of clicked slot is 14:
            zcratemaker_gui_itemeditor(player, {_crate})
    else if name of player's current inventory contains "&8Crate Key Edit | &6&l":
        cancel event
        clicked inventory is player's current inventory
        set {_crate} to name of player's current inventory
        replace all "&8Crate Key Edit | &6&l" in {_crate} with ""
        if index of clicked slot is 26:
            zcratemaker_gui_edit(player, {_crate})
        else if index of clicked slot is 12:
            click action is left mouse button 
            if player's tool is air:
                send "&cYou cannot set the crate item to air."
                stop
            set {zcratemaker::crates::%{_crate}%::cratekeyitem} to player's tool's type with all flags hidden
            send "&aYou have successfully updated the crate item of %{_crate}% to %{zcratemaker::crates::%{_crate}%::cratekeyitem}%"
            zcratemaker_gui_editcratekey(player, {_crate})
        else if index of clicked slot is 13:
            click action is left mouse button 
            zcratemaker_sendchatquery(player, {_crate}, "cratekeyedit-name")
        else if index of clicked slot is 14:
            click action is left mouse button 
            zcratemaker_sendchatquery(player, {_crate}, "cratekeyedit-lore")
    else if name of player's current inventory contains "&8Crate Item Editor | &6&l":
        cancel event
        clicked inventory is player's current inventory
        set {_crate} to name of player's current inventory
        replace all "&8Crate Item Editor | &6&l" in {_crate} with ""

        if index of clicked slot is 45:
            zcratemaker_gui_edit(player, {_crate})
        
        set {_slot} to -1
        loop {zcratemaker::crates::%{_crate}%::items::*}:
            add 1 to {_slot}
            if clicked slot is {_slot}:
                send "&cYou have deleted &4&n%loop-value%&c from the &n%{_crate}% Crate&c."
                delete {zcratemaker::crates::%{_crate}%::items::%loop-index%}
                zcratemaker_gui_itemeditor(player, {_crate})
                stop
        
    else if name of player's current inventory contains "&8Crate Gui Edit | &6&l":
        cancel event
        clicked inventory is player's current inventory
        set {_crate} to name of player's current inventory
        replace all "&8Crate Gui Edit | &6&l" in {_crate} with ""
        if index of clicked slot is 22:
            zcratemaker_gui_edit(player, {_crate})
        else if index of clicked slot is 11:
            click action is left mouse button
            if {zcratemaker::crates::%{_crate}%::guicrateshow} is false:
                set {zcratemaker::crates::%{_crate}%::guicrateshow} to true
                send "&aYou have enabled %{_crate}%'s crate appearance."
            else:
                set {zcratemaker::crates::%{_crate}%::guicrateshow} to false
                send "&cYou have disabled %{_crate}%'s crate appearance."
            zcratemaker_gui_editcrategui(player, {_crate})
        else if index of clicked slot is 12:
            click action is left mouse button 
            if player's tool's type is {zcratemaker::crates::%{_crate}%::guicrateitem}:
                send "&cThe crate key item is already set to this."
                stop
            if player's tool is air:
                send "&cYou cannot set the crate item to air."
                stop
            set {zcratemaker::crates::%{_crate}%::guicrateitem} to player's tool's type
            send "&aYou have successfully updated the crate gui item of %{_crate}% to %{zcratemaker::crates::%{_crate}%::cratekeyitem}%"
            zcratemaker_gui_editcrategui(player, {_crate})
        else if index of clicked slot is 13:
            click action is left mouse button 
            zcratemaker_sendchatquery(player, {_crate}, "crateguiedit-name")
        else if index of clicked slot is 14:
            click action is left mouse button 
            zcratemaker_sendchatquery(player, {_crate}, "crateguiedit-lore")
        else if index of clicked slot is 15:
            click action is left mouse button 
            zcratemaker_sendchatquery(player, {_crate}, "crateguiedit-slot")